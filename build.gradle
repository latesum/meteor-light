apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'kotlin-allopen'
apply plugin: 'kotlin-spring'
apply plugin: 'idea'
apply plugin: 'com.google.protobuf'
apply plugin: 'org.springframework.boot'

group 'com.latesum'
version '0.0.1-SNAPSHOT'

jar {
    baseName = 'meteor-light'
    version = '0.0.1-SNAPSHOT'
}

buildscript {
    ext {
        // Global version variable.
        kotlinVersion = '1.1.1'
        springBootVersion = '1.5.2.RELEASE'
        grpcVersion = '1.1.2'
    }
    repositories {
        maven {
            url "http://mvn.pat-edu.org:8080/artifactory/mavencentral/"
        }
        maven {
            url "http://mvn.pat-edu.org:8080/artifactory/jcenter/"
        }
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:$springBootVersion"
        classpath "org.jetbrains.kotlin:kotlin-allopen:$kotlinVersion"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.0'
    }
}

// Java compatibility.
sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    maven {
        url "http://mvn.pat-edu.org:8080/artifactory/mavencentral/"
    }
    maven {
        url "http://mvn.pat-edu.org:8080/artifactory/jcenter/"
    }
    maven {
        url "http://mvn.pat-edu.org:8080/artifactory/pat/"
    }
}

// Add Kotlin and protobuf generated codes into source sets.
sourceSets {
    main.java.srcDirs += 'src/main/kotlin'
    main.java.srcDirs += file("${protobuf.generatedFilesBaseDir}/main/java")
    main.java.srcDirs += file("${protobuf.generatedFilesBaseDir}/main/grpc")
}

dependencies {
    // gRPC
    compile "io.grpc:grpc-all:$grpcVersion"
    // Spring
    compile "org.springframework.boot:spring-boot-starter:$springBootVersion"
    compile "org.springframework.boot:spring-boot-starter-data-jpa:$springBootVersion"
    compile "org.springframework.boot:spring-boot-starter-data-redis:$springBootVersion"
    compile "org.springframework.boot:spring-boot-starter-jdbc:$springBootVersion"
    compile "org.springframework.boot:spring-boot-starter-web:$springBootVersion"
    compile "org.springframework.boot:spring-boot-starter-mail:$springBootVersion"
    compile "org.springframework.boot:spring-boot-starter-security:$springBootVersion"
    // Hibernate ORM
    compile 'org.hibernate:hibernate-core:5.2.8.Final'
    // gRPC x Spring
    compile 'org.lognet:grpc-spring-boot-starter:2.0.0'
    // Guava
    compile 'com.google.guava:guava:20.0'
    // redis-session
    compile 'org.springframework.session:spring-session:1.3.0.RELEASE'
    // Kotlin
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
    // commons-io
    compile 'commons-io:commons-io:2.5'
    // Protocol Buffer
    compile 'com.google.protobuf:protobuf-java:3.2.0'
    // gson
    compile 'com.google.code.gson:gson:2.8.1'
    // crawler4j
    compile 'edu.uci.ics:crawler4j:4.2'
    // Apache commons
    compile 'org.apache.commons:commons-pool2:2.4.2'
    compile 'org.apache.commons:commons-lang3:3.5'
    // Apache httpcomponents
    compile 'org.apache.httpcomponents:httpclient:4.5.3'
    compile 'org.apache.httpcomponents:httpmime:4.5.3'
    // Apache POI
    compile 'org.apache.poi:poi:3.15'
    compile 'org.apache.poi:poi-ooxml:3.15'
    // Redisson
    compile 'de.ruedigermoeller:fst:2.50'
    compile 'org.redisson:redisson:3.3.2'
    // MySQL
    runtime 'mysql:mysql-connector-java:6.0.5'

}

// Compile protos before kotlin.
// For compiling protos manually, run `gradlew :generateProto`.
compileKotlin {
    dependsOn(':generateProto')
}

allOpen {
    annotation('javax.persistence.Entity')
    annotation('org.springframework.boot.test.context.SpringBootTest')
}

protobuf {
    protoc {
        // The artifact spec for the Protobuf Compiler
        artifact = 'com.google.protobuf:protoc:3.2.0'
    }
    plugins {
        // an artifact spec for a protoc plugin, with "grpc" as the identifier, which can
        // be referred to in the "plugins" container of the "generateProtoTasks" closure.
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:$grpcVersion"
        }
    }
    // https://github.com/google/protobuf/issues/2054
    generateProtoTasks {
        all()*.plugins {
            grpc {}
        }
        all().each { task ->
            task.doLast {
                ant.replaceregexp(
                        match: '@java.lang.Deprecated|@deprecated',
                        replace: '',
                        flags: 'g',
                        byline: true
                ) {
                    fileset(dir: "${protobuf.generatedFilesBaseDir}/main/java")
                }
            }
        }
    }
}

idea {
    module {
        sourceDirs += file("${protobuf.generatedFilesBaseDir}/main/java")
        sourceDirs += file("${protobuf.generatedFilesBaseDir}/main/grpc")
    }
}
